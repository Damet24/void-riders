## Sistema de configuraciones del juego
##
## Este script gestiona toda la configuraci贸n del juego, incluyendo
## la creaci贸n, carga y guardado de las preferencias del usuario.
##
## Funcionalidades principales:
## - Carga autom谩tica del archivo de configuraci贸n al iniciar el juego.
## - Generaci贸n de una configuraci贸n por defecto si no existe.
## - Lectura y escritura de valores en secciones (Display, Audio, Controls).
## - Emisi贸n de se帽al `setting_changed` al modificar un valor.
## - Permite restaurar la configuraci贸n a valores por defecto.
##
## ```gdscript
## Settings.save_setting(Settings.SECTION.AUDIO, Settings.AUDIO_KEYS.MUSIC_VOLUME, 0.8)
## var vol = Settings.get_setting(Settings.SECTION.AUDIO, Settings.AUDIO_KEYS.MASTER_VOLUME)
## ```

extends Node

## Se帽al emitida cuando una configuraci贸n cambia.
## 
## @param section (String) - Secci贸n del archivo modificada.
## @param key (String) - Clave dentro de la secci贸n modificada.
## @param value (Variant) - Nuevo valor asignado.
signal setting_changed(section: String, key: String, value: Variant)

## Ruta del archivo donde se almacenan las configuraciones del usuario.
const SETTINGS_FILE_PATH := "user://settings.ini"

## Instancia del archivo de configuraci贸n cargado en memoria.
var config := ConfigFile.new()

## Secciones principales del archivo de configuraci贸n.
const SECTION := {
	DISPLAY = "Display",
	AUDIO = "Audio",
}

## Claves correspondientes a las opciones de pantalla.
const DISPLAY_KEYS := {
	FULLSCREEN = "Fullscreen",
}

## Claves correspondientes a las opciones de audio.
const AUDIO_KEYS := {
	MASTER_VOLUME = "MasterVolume",
	MUSIC_VOLUME = "MusicVolume",
	SFX_VOLUME = "SFXVolume"
}


## Inicializa el sistema de configuraci贸n del juego.
##
## Si el archivo de configuraci贸n no existe, se crea uno con valores por defecto.
## Si existe, se carga en memoria para su uso durante la sesi贸n.
func _ready() -> void:
	if not FileAccess.file_exists(SETTINGS_FILE_PATH):
		_create_default_settings()
	else:
		config.load(SETTINGS_FILE_PATH)
	
	_apply_display_settings()
	_apply_audio_settings()
	setting_changed.connect(_sync_settings)

## Crea una configuraci贸n inicial con valores por defecto.
##
## Este m茅todo se ejecuta autom谩ticamente si no se encuentra el archivo
## `settings.ini` en el directorio del usuario.
func _create_default_settings() -> void:
	#region DISPLAY
	config.set_value(SECTION.DISPLAY, DISPLAY_KEYS.FULLSCREEN, true)
	#endregion

	#region AUDIO
	config.set_value(SECTION.AUDIO, AUDIO_KEYS.MASTER_VOLUME, 1.0)
	config.set_value(SECTION.AUDIO, AUDIO_KEYS.MUSIC_VOLUME, 1.0)
	config.set_value(SECTION.AUDIO, AUDIO_KEYS.SFX_VOLUME, 1.0)
	#endregion

	config.save(SETTINGS_FILE_PATH)

## Guarda un valor en una secci贸n espec铆fica del archivo de configuraci贸n.
##
## @param section (String) - Nombre de la secci贸n (por ejemplo `SECTION.AUDIO`)
## @param key (String) - Clave dentro de la secci贸n (por ejemplo `AUDIO_KEYS.MUSIC_VOLUME`)
## @param value (Variant) - Valor que se desea guardar
##
##  Tambi茅n emite la se帽al `setting_changed` para notificar a otros nodos.
func save_setting(section: String, key: String, value: Variant) -> void:
	config.set_value(section, key, value)
	config.save(SETTINGS_FILE_PATH)
	emit_signal("setting_changed", section, key, value)

## Obtiene el valor de una configuraci贸n almacenada.
##
## @param section (String) - Secci贸n del archivo
## @param key (String) - Clave dentro de la secci贸n
## @param default_value (Variant) - Valor por defecto si no existe la clave
##
## @return Variant - El valor almacenado, o `default_value` si no existe
func get_setting(section: String, key: String, default_value: Variant = null) -> Variant:
	return config.get_value(section, key, default_value)


func save_display_setting(key: String, value: Variant) -> void:
	save_setting(SECTION.DISPLAY, key, value)


func get_display_settings(key: String, default_value: Variant = null) -> Variant:
	return get_setting(SECTION.DISPLAY, key, default_value)


func save_audio_setting(key: String, value: Variant) -> void:
	save_setting(SECTION.AUDIO, key, value)


func get_audio_settings(key: String, default_value: Variant = null) -> Variant:
	return get_setting(SECTION.AUDIO, key, default_value)


## Restaura todas las configuraciones a sus valores por defecto.
##
## Borra el archivo actual y genera uno nuevo con la configuraci贸n inicial.
func reset_to_defaults() -> void:
	config = ConfigFile.new()
	_create_default_settings()
	setting_changed.emit(SECTION.DISPLAY, DISPLAY_KEYS.FULLSCREEN, get_setting(SECTION.DISPLAY, DISPLAY_KEYS.FULLSCREEN))
	_apply_display_settings()
	_apply_audio_settings()


func _apply_display_settings():
	var fullscreen = get_setting(SECTION.DISPLAY, DISPLAY_KEYS.FULLSCREEN, true)
	DisplayServer.window_set_mode(DisplayServer.WINDOW_MODE_FULLSCREEN if fullscreen else DisplayServer.WINDOW_MODE_WINDOWED)


func _apply_audio_settings():
	var master = get_setting(SECTION.AUDIO, AUDIO_KEYS.MASTER_VOLUME, 1.0)
	AudioServer.set_bus_volume_db(AudioServer.get_bus_index("Master"), linear_to_db(master ))

	var music = get_setting(SECTION.AUDIO, AUDIO_KEYS.MUSIC_VOLUME, 1.0)
	AudioServer.set_bus_volume_db(AudioServer.get_bus_index("Music"), linear_to_db(music))

	var sfx = get_setting(SECTION.AUDIO, AUDIO_KEYS.SFX_VOLUME, 1.0)
	AudioServer.set_bus_volume_db(AudioServer.get_bus_index("SFX"), linear_to_db(sfx))


func _sync_settings(section: String, key: String, value: Variant) -> void:
	match section:
		SECTION.DISPLAY: _sync_display_settings(key, value)
		SECTION.AUDIO: _sync_audio_settings(key, value)

func _sync_display_settings(key: String, value: Variant) -> void:
	if key == DISPLAY_KEYS.FULLSCREEN:
		DisplayServer.window_set_mode(DisplayServer.WINDOW_MODE_FULLSCREEN if value else DisplayServer.WINDOW_MODE_WINDOWED)

func _sync_audio_settings(key: String, value: Variant) -> void:
	var bus_name := ""
	match key:
		AUDIO_KEYS.MASTER_VOLUME: bus_name = "Master"
		AUDIO_KEYS.MUSIC_VOLUME: bus_name = "Music"
		AUDIO_KEYS.SFX_VOLUME: bus_name = "SFX"
	if bus_name:
		AudioServer.set_bus_volume_db(AudioServer.get_bus_index(bus_name), linear_to_db(value))
